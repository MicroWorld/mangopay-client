buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'de.thetaphi:forbiddenapis:2.6'
    }
}

plugins {
    id 'java-library'
    id 'jacoco'
    id 'pmd'
    id 'findbugs'
    id 'maven'
    id 'idea'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'com.simonharrer.modernizer' version '1.6.0-1'
    id 'de.aaschmid.cpd' version '1.2'
    id 'net.researchgate.release' version '2.8.0'
}

// Old style plugin
// https://github.com/policeman-tools/forbidden-apis/wiki/GradleUsage
apply plugin: 'de.thetaphi.forbiddenapis'

group = 'org.microworld'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    integration {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    compile.transitive = false
    compileClasspath.transitive = false
    integrationCompile.extendsFrom testCompile
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
    integrationRuntimeOnly.extendsFrom testRuntimeOnly
    deployerJars
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'org.apache.commons:commons-lang3:3.8.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-junit:2.0.0.0'
    testImplementation 'io.codearte.jfairy:jfairy:0.5.9'
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.3'

    deployerJars 'org.apache.maven.wagon:wagon-ssh:3.3.1'
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.setDeprecation true
}

task integration(type: Test) {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    mustRunAfter test
}
check.dependsOn integration

license {
    header = file('LICENSE-HEADER.txt')
    exclude('**/*.pdf')
}

forbiddenApis {
    failOnUnsupportedJava = false
    bundledSignatures = [ 'jdk-unsafe', 'jdk-deprecated', 'jdk-system-out', 'jdk-non-portable' ]
}

jacoco {
    toolVersion = '0.8.2'
}

findbugs {
    ignoreFailures = true
    effort = 'max'
    reportLevel = 'low'
}

cpdCheck {
    ignoreFailures = true
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: 'sftp://git@scm.abcmicrofinance.net/home/git/maven/release')
            snapshotRepository(url: 'sftp://git@scm.abcmicrofinance.net/home/git/maven/snapshot')
        }
    }
}

release {
    buildTasks = ['assemble']
    tagTemplate = '$name-$version'
    git {
        requireBranch = 'master'
    }
}

afterReleaseBuild.dependsOn uploadArchives

idea {
    module {
        testSourceDirs += file('src/integration/java')
    }
}
